plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.devtools.ksp'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
}

android {
    namespace 'com.example.appshell'
    compileSdk 33

    signingConfigs {
        release {
            storeFile file("../appshell.jks")
            storePassword "123456"
            keyAlias "key0"
            keyPassword "123456"
        }
    }

    defaultConfig {
        applicationId "com.example.appshell"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName app_version

        // 不用 x5 内核，使用系统内核也是可以打开网页。目前测试无差别。
        // tbs FAQ：
        // 不给 x86 下载内核，设置以下无效。
        // 内核使用下载，所以必须联网。
//        ndk {
//            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
//        }


        // 腾讯IM
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        // 必须 HTTPS
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue("string", "app_name", "AppShell 正式版")
            resValue("bool", "http_allow", "false")

            buildConfigField("int", "APP_TX_APP_ID", "1400805216")

            signingConfig signingConfigs.release
        }

        // debug 允许 http
        debug {
            applicationIdSuffix ".debug"
            debuggable true

            resValue("string", "app_name", "AppShell 调试版")
            resValue("bool", "http_allow", "true")

            buildConfigField("int", "APP_TX_APP_ID", "1400805216")

            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
//        sourceCompatibility JavaVersion.VERSION_11
//        targetCompatibility JavaVersion.VERSION_11
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
//        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_ui_version"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    // 分发维度，每加一个标签，就多一个维度。
    // 比如又 3个 flavors 是 mode , 4 个 flavors 是 api， 就会有 12 种分发版本
    // Build > Select Build Variant 切换
    // 注：修改配置会有缓存问题（比如：加了维度，然后没有显示出来），清理缓存重启。
    flavorDimensions "mode", "platform"

    productFlavors {
        online {
            dimension "mode"

        }

        offline {
            dimension "mode"
        }

        normal {
            dimension "platform"
        }

        googlePlay {
            dimension "platform"
        }
        xiaomiAppStore {
            dimension "platform"
        }
    }
    sourceSets {
        main {
            assets {
                srcDirs 'src\\main\\assets'
            }
        }
    }

    // 遍历所有构造变量
    applicationVariants.all { variant ->
        // 找到输出相关的变量
        variant.outputs.all { output ->
            // 替换掉输出变量的文件名
            output.outputFileName = "${variant.getFlavorName()}-${variant.buildType.name}-${versionName}-${new Date().format('yyyyMMdd')}-${output.getFilter(com.android.build.OutputFile.ABI)}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.0'

    // 时间扩展库，依赖 java 8 新的 LocalDateTime 类，需要 安卓 26（安卓 8.0） 以上版本
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.4.0'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // 扩展库
    api "com.google.accompanist:accompanist-insets:$accompanist_version"
    api "com.google.accompanist:accompanist-insets-ui:$accompanist_version"
    api "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    api "com.google.accompanist:accompanist-pager:$accompanist_version"
    api "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    api "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"

    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"

    // 路由导航
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    // prefecence android 10 以上使用扩展库提供，旧同名方法被标记为弃用。
    // implementation 'androidx.preference:preference:1.2.0' // JAVA
    implementation 'androidx.preference:preference-ktx:1.2.0' // KT

    // rx
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'

    // 图片显示
    api "io.coil-kt:coil-compose:2.2.0"

    // BSON
    implementation 'org.mongodb:bson:4.9.0'

    // x5
    api 'com.tencent.tbs:tbssdk:44286'

    // 微信 SDK
    api 'com.tencent.mm.opensdk:wechat-sdk-android:6.8.24'

    // txim
//    api 'com.tencent.imsdk:imsdk:5.1.66' // 基础版，啥都没有，就几个简单接口。。。而且很久没更新。
    api 'com.tencent.imsdk:imsdk-plus:7.1.3933'
//    api 'com.tencent.imsdk:imsdk-plus-pro:版本号'

    // room
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    // To use Kotlin Symbol Processing (KSP)
    ksp "androidx.room:room-compiler:$room_version"

    // ktor
    api "io.ktor:ktor-client-android:$ktor_version"
    api "io.ktor:ktor-client-cio:$ktor_version"
//    api "io.ktor:ktor-client-serialization:$ktor_version"
    api "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    api "io.ktor:ktor-client-content-negotiation:$ktor_version"

    testImplementation 'junit:junit:4.13.2'
    // optional - Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}